program = block$;

block = {statement};

statement = ifStatement | whileStatement | callStatement | var | reassignment ;

var = "var" identifier "=" (expression|string);

reassignment = identifier "=" (expression|string);

ifStatement = "if" "(" expression ")"  "{" block "}" ["else" "{" block "}"];

whileStatement = "while" "(" expression ")"  "{" block "}";

callStatement = ("System." | "Turtle.") identifier "(" [paramList] ")";

paramList =  param {"," param};

param = (expression|string);

expression = equality;

equality = comparison {("!=" | "==") comparison}; 

comparison = term {( ">=" | ">" | "<=" | "<") term};

term = factor {("+" | "-") factor};

factor= unary {("*"|"/"|"%") unary};

unary = ["-" | "+"] unary | primary;

primary = identifier | number | "(" expression ")";

identifier = /[A-Za-z\_]+/;

number = /[+-]?([0-9]+([.][0-9]*)?|[.][0-9]+)/;

string = /"[^"]*"/;