program = block$;

block = {statement};

statement = ifStatement | whileStatement | callStatement | var | deklarace ;

var = "var" identifier "=" (expression|string);

deklarace = identifier "=" (expression|string);

ifStatement = "if" "(" condition ")"  "{" block "}" ["else" "{" block "}"];

whileStatement = "while" "(" condition ")"  "{" block "}";

callStatement = ("System." | "Turtle.") identifier "(" (expression | string ) ")";

condition = expression ("=="| "!=" | ">=" | ">" | "<=" | "<") expression;

expression = ["-" | "+"] term {("+" | "-") term};

term = factor {("*"|"/"|"%") factor};

factor = identifier | number | "(" expression ")";

identifier = /[A-Za-z\_]+/;

number = /[+-]?([0-9]+([.][0-9]*)?|[.][0-9]+)/;

string = /"[^"]*"/;